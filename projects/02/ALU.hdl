// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Xor16(a=x, b=x, out=zero);
    Mux16(a=x, b=false, sel=zx, out=zerox);
    Mux16(a=y, b=false, sel=zy, out=zeroy);
    Not16(in=zerox, out=negatedx16);
    Not16(in=zeroy, out=negatedy16);
    Mux16(a=zerox, b=negatedx16, sel=nx, out=negatedx);
    Mux16(a=zeroy, b=negatedy16, sel=ny, out=negatedy);

    And16(a=negatedx, b=negatedy, out=xandy);
    Add16(a=negatedx, b=negatedy, out=xplusy);
    Mux16(a=xandy, b=xplusy, sel=f, out=preout);

    Not16(in=preout, out=notpreout);
    Mux16(a=preout, b=notpreout, sel=no, out=out, out[0..7]=oz1, out[8..15]=oz2, out[15]=ng);

    Or8Way(in=oz1, out=zr1);
    Or8Way(in=oz2, out=zr2);
    Or(a=zr1, b=zr2, out=zr3);
    Not(in=zr3, out=zr);
}